import NotePadModel.Note

application AndroTextExamples {
	
	listactivity ExamplesListActivity {
		listitem simple_list_item
		fetchAll from exampleNameListProvider
	}
	resourceprovider exampleNameListProvider resource exampleResources.exampleNameList
	
	// Hello World
	activity HelloWorldActivity {
		layout hellolayout
	}
	
	// Hello AutoComplete Example
	activity HelloAutoCompleteActivity {
		layout main
	} 
	
	// Hello GridView Example
	activity HelloGridViewActivity {
		layout gridView
		// in the generated class HelloGridViewActivity load the GridView contents, like in the Hello GridView article
		// http://developer.android.com/guide/tutorials/views/hello-gridview.html
	}
	
	// Hello LinearLauyout Example
	activity HelloLinearLayoutActivity {
		layout helloLinearLayoutMain
	}

	// Hello Relative Layout Example
	activity HelloRelativeLayoutExampleActivity {
		layout relativeLayout
	}
	
	// Hello TableLayout Example
	activity HelloTableLayoutActivity {
		layout table
	}

	// Hello ListActivity Example
	listactivity HelloListActivity {
		listitem simple_list_item
		fetchAll from countriesResourceProvider
	}
	resourceprovider countriesResourceProvider resource HelloListActivityResources.countries
	
	// Hello TabActivity Example
	tabactivity HelloTabWidgetActivity {
		
		tab "artists" {
			show ArtistsActivity
			image HelloTabWidgetRes.ic_tab_image
		}
		
		tab "albums" {
			show AlbumsActivity
			image HelloTabWidgetRes.ic_tab_image
		}
		
		tab "songs" {
			show SongsActivity
			image HelloTabWidgetRes.ic_tab_image
		}
	    	
	}	
	activity ArtistsActivity {
		layout artistsText
	}
	activity AlbumsActivity {
		layout albumsText
	}
	activity SongsActivity {
		layout songsText
	}
	
	// Hello Simple Navigation Example
	activity MainActivity {
		layout mainLayout
	}
	activity OtherActivity {
		layout otherLayout
	}
	
	// Hello Activity Menu Example
	activity MenuActivity {
		layout menulayout
		menu simplemenu {
			group common {
				item save "Save" ic_menu_save
				item edit "Edit" ic_menu_edit
			}
			item upload "Upload" ic_menu_upload
			item more "More" ic_menu_more {
				menu submenu {
					item moreItem "MoreMoreMore" ic_menu_help
				}
			}
		}
	}
	
	// Simple Data Example
	listactivity DataExampleActivity {
		listitem simple_list_item
		fetchAll Note(note) from SimpleDataProvider
		bind simple_list_item
	}

	provider SimpleDataProvider data NotePadModel
	
	// Simple Preferences
	preferences Settings {
		layout preferencescreen "Settings" {
			preferencecategory "SimpleCategory" {
				checkboxpreference simpleCheckBox "This is a CheckboxPreference"
				edittextpreference simpleEditText "This is an EditTextPreference"				
			}
			preference appVersion "Version 1.0"
		}
	}
}